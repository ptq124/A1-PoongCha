buildscript {
    ext {
        restdocsApiSpecVersion = '0.16.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.poongcha'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

repositories {
    mavenCentral()
}

openapi3 {
    setServer("http://localhost:8080")
    title = "내 차 만들기 API 명세서"
    description = "내 차 만들기 API 명세서"
    version = "0.0.1"
    format = "yaml"
}

swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    // car 서비스 모듈
    implementation project(':car')

    // recommend 서비스 모듈
    implementation project(':recommend')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    swaggerUI 'org.webjars:swagger-ui:4.11.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('prepareKotlinBuildScriptModel')

// GenerateSwaggerUI 태스크가, openapi3 task 를 의존하도록 설정
tasks.withType(GenerateSwaggerUI) {
    dependsOn 'openapi3'
}

// 생성된 SwaggerUI 를 jar 에 포함시키기 위해 build/resources 경로로 로 복사
tasks.register('copySwaggerUI', Copy) {
    dependsOn jar
    dependsOn 'generateSwaggerUISample'

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()

    copy {
        from("${generateSwaggerUISampleTask.outputDir}")
        into("${project.buildDir}/resources/main/static/docs")
    }
    copy {
        from "${project(':car').buildDir}/resources/main/static/docs"
        into "${buildDir}/resources/main/static/car"
    }
    copy {
        from "${project(':recommend').buildDir}/resources/main/static/docs"
        into "${buildDir}/resources/main/static/recommend"
    }
}

bootJar {
    bootJarMainClassName.dependsOn copySwaggerUI
}
